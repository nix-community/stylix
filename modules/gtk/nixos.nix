{
  mkTarget,
  pkgs,
  lib,
  ...
}:
mkTarget {
  options.extraCss = lib.mkOption {
    description = ''
      Extra code added to `gtk-3.0/gtk.css` and `gtk-4.0/gtk.css`.
    '';
    type = lib.types.lines;
    default = "";
    example = ''
      // Remove rounded corners
      window.background { border-radius: 0; }
    '';
  };

  config = [
    {
      # Required for Home Manager's GTK settings to work
      programs.dconf.enable = true;
    }

    (
      { cfg, colors }:
      let
        baseCss = colors {
          template = ./gtk.css.mustache;
          extension = ".css";
        };

        theme =
          pkgs.runCommandLocal "${colors.slug}-gtk-theme"
            {
              inherit baseCss;
              inherit (cfg) extraCss;
              parent = "${pkgs.adw-gtk3}/share/themes/adw-gtk3";
            }
            ''
              config="$out/etc/xdg"
              theme="$out/share/themes/Stylix"

              mkdir --parents \
                "$config/gtk-3.0" \
                "$config/gtk-4.0" \
                "$theme/gtk-3.0" \
                "$theme/gtk-4.0"

              cat >"$config/gtk-3.0/settings.ini" <<EOF
              [Settings]
              gtk-theme-name=Stylix
              EOF

              cat >"$config/gtk-4.0/settings.ini" <<EOF
              [Settings]
              gtk-theme-name=Stylix
              EOF

              cat >"$theme/index.theme" <<EOF
              [Desktop Entry]
              Name=Stylix
              Type=X-GNOME-Metatheme
              Comment=Theme generated by Stylix based on Stylix
              Encoding=UTF-8

              [X-GNOME-Metatheme]
              GtkTheme=Stylix
              EOF

              cat >"$theme/gtk-3.0/gtk.css" <<EOF
              @import url('$parent/gtk-3.0/gtk.css');
              @import url('$baseCss');
              $extraCss
              EOF

              cat >"$theme/gtk-3.0/gtk-dark.css" <<EOF
              @import url('$parent/gtk-3.0/gtk-dark.css');
              @import url('$baseCss');
              $extraCss
              EOF

              ln --symbolic \
                "$parent/gtk-3.0/assets" \
                "$theme/gtk-3.0/assets"

              cat >"$theme/gtk-4.0/gtk.css" <<EOF
              @import url('$parent/gtk-4.0/gtk.css');
              @import url('$baseCss');
              $extraCss
              EOF

              cat >"$theme/gtk-4.0/gtk-dark.css" <<EOF
              @import url('$parent/gtk-4.0/gtk-dark.css');
              @import url('$baseCss');
              $extraCss
              EOF

              ln --symbolic \
                "$parent/gtk-4.0/assets" \
                "$theme/gtk-4.0/assets"
            '';
      in
      {
        environment.systemPackages = [ theme ];

        # Under GNOME on Wayland, the XDG Desktop Portal implementation takes
        # priority over the settings files in `$XDG_CONFIG_DIRS/etc/xdg` [1],
        # reading from this DConf path instead.
        #
        # [1] https://docs.gtk.org/gtk4/class.Settings.html
        programs.dconf.profiles.user.databases = [
          { settings."org/gnome/desktop/interface".gtk-theme = "Stylix"; }
        ];
      }
    )
  ];
}
